plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
	withSourcesJar()
	withJavadocJar()
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = 'DevLogin'
				artifactId = 'devlogin'
				description = 'Login with your own Minecraft account in a Fabric mod development environment.'
				url = 'https://github.com/PlanetTeamSpeakk/DevLogin'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://raw.githubusercontent.com/PlanetTeamSpeakk/DevLogin/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'PlanetTeamSpeak'
						name = 'PlanetTeamSpeak'
						email = 'PlanetTeamSpeakk@users.noreply.github.com'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/PlanetTeamSpeakk/DevLogin.git'
					developerConnection = 'scm:git:git@github.com:PlanetTeamSpeakk/DevLogin.git'
					url = 'https://github.com/PlanetTeamSpeakk/DevLogin'
				}
				issueManagement {
					system = "GitHub"
					url = "https://github.com/PlanetTeamSpeakk/DevLogin/issues"
				}
			}
			// add all the jars that should be included when publishing to maven
			artifact(javadocJar) {
				builtBy javadocJar
			}
			artifact(jar) {
				builtBy jar
			}
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		mavenLocal()
		maven {
			name "MavenCentral"
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username ossrhUsername
				password ossrhPassword
			}
		}
		maven {
			name "GithubPackages"
			url ghUrlRoot + "/DevLogin"
			credentials {
				username ghUsername
				password ghToken
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}