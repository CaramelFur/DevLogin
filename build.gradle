plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.6"
    id "maven-publish"
    id "signing"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()

        // https://mvnrepository.com/artifact/org.asynchttpclient/async-http-client
        implementation "org.asynchttpclient:async-http-client:2.12.3"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 8
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    def fabricSources = project(":fabric").sourcesJar
    def forgeSources = project(":forge").sourcesJar
    def quiltSources = project(":quilt").sourcesJar

    dependsOn commonSources
    dependsOn fabricSources
    dependsOn forgeSources
    dependsOn quiltSources

    from commonSources.archiveFile.map { zipTree(it) }
    from fabricSources.archiveFile.map { zipTree(it) }
    from forgeSources.archiveFile.map { zipTree(it) }
    from quiltSources.archiveFile.map { zipTree(it) }
}

def exportedProjects = [":", ":common", ":fabric", ":forge", ":quilt"]
task allJavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
}

javadocJar {
    from allJavadoc
}

forgix {
    group = "com.ptsmods.devlogin"
    mergedJarName = "DevLogin-${version}.jar"
    outputDir = "build/libs"
}

def mergedJarFile = layout.buildDirectory.file("libs/DevLogin-${version}.jar")
def mergedJarArtifact = artifacts.add("archives", mergedJarFile.get().asFile)

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'DevLogin'
                artifactId = 'devlogin'
                description = 'Login with your own Minecraft account in a mod development environment.'
                url = 'https://github.com/PlanetTeamSpeakk/DevLogin'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/PlanetTeamSpeakk/DevLogin/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'PlanetTeamSpeak'
                        name = 'PlanetTeamSpeak'
                        email = 'PlanetTeamSpeakk@users.noreply.github.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/PlanetTeamSpeakk/DevLogin.git'
                    developerConnection = 'scm:git:git@github.com:PlanetTeamSpeakk/DevLogin.git'
                    url = 'https://github.com/PlanetTeamSpeakk/DevLogin'
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/PlanetTeamSpeakk/DevLogin/issues"
                }
            }
            // add all the jars that should be included when publishing to maven
            artifact mergedJarArtifact
            artifact(javadocJar) {
                builtBy javadocJar
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name "MavenCentral"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
        maven {
            name "GithubPackages"
            url ghUrlRoot + "/DevLogin"
            credentials {
                username ghUsername
                password ghToken
            }
        }
    }
}

tasks.publish.dependsOn(mergeJars)

signing {
    sign publishing.publications.mavenJava
}
